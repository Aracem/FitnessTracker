buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.4'
    }
}
apply plugin: 'android'

configurations {
    preprocess
}

dependencies {
    compile files('libs/GraphView-3.1.1.jar')
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.path:android-priority-jobqueue:0.9.9'
    preprocess 'com.squareup.dagger:dagger-compiler:1.2.0'
    compile 'com.squareup.dagger:dagger:1.2.0'
    preprocess 'com.jakewharton:butterknife:4.0.1'
    compile 'com.jakewharton:butterknife:4.0.1'
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // make sure that lint errors don't fail the build as lint does not always work properly with the android gradle plugin
    lintOptions {
        abortOnError false
    }
}

android.applicationVariants.all { variant ->
    //Where to place the generated code
    variant.ext.set("gen_${variant.dirName}", file("${project.buildDir}/source/gen/${variant.dirName}"))

    //Do the actual preprocessor things
    variant.javaCompile.doFirst {
        File out = variant.ext.get("gen_${variant.dirName}")
        out.mkdirs()
        variant.javaCompile.options.compilerArgs += [
                '-processorpath', configurations.preprocess.getAsPath(),
                '-s', out
        ]
    }
}
